plugins {
    id 'base'
}

version = project.mod_version
group = project.maven_group

allprojects {
    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }
    }
}

subprojects {
    tasks.register('purgeDuplicateBuildArtifacts') {
        doLast {
            if (!buildDir.exists()) return

            delete fileTree(buildDir) {
                include '**/* 2*'
                include '**/.DS_Store'
            }

            // Remove any now-empty duplicate-named directories.
            fileTree(buildDir).visit { details ->
                if (details.file.isDirectory() && details.file.name.contains(' 2')) {
                    details.file.deleteDir()
                }
            }
        }
    }

    tasks.matching { it.name == 'build' || it.name == 'processResources' }.configureEach {
        dependsOn(tasks.named('purgeDuplicateBuildArtifacts'))
    }
}

tasks.named('build') {
    dependsOn(':fabric:build')
    dependsOn(':neoforge:build')
}
